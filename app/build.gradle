plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

android {
    namespace 'su.th2empty.kutts'
    compileSdk 33

    def developmentStages = [
            dev: "-dev",
            beta: "-beta",
            production: ""
    ]

    def currentStage = "dev"

    defaultConfig {
        applicationId "su.th2empty.kutts"
        minSdk 26
        targetSdk 33
        versionCode getLatestCommitNumber()
        versionName "${versionMajor}.${versionMinor}.${versionPatch}" + developmentStages[currentStage]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
            applicationVariants.all { variant ->
                 variant.outputs.all {
                     outputFileName = "kutts-${variant.versionName}.apk"
                 }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

static def getLatestCommitNumber() {
    def process = "git rev-list --count HEAD".execute()
    process.text.trim().toInteger()
}

dependencies {
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    implementation "androidx.room:room-runtime:2.5.1"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    annotationProcessor "androidx.room:room-compiler:2.5.1"

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}